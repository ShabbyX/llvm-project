import("//build_overrides/llvm.gni")

import("//${llvm_root}llvm/lib/Target/targets.gni")

static_library("Rewrite") {
  output_name = "LLVMBOLTRewrite"
  configs += [ "//${llvm_build_root}build:bolt_code" ]
  deps = [
    "//${llvm_root}bolt/lib/Core",
    "//${llvm_root}bolt/lib/Passes",
    "//${llvm_root}bolt/lib/Profile",
    "//${llvm_root}bolt/lib/RuntimeLibs",
    "//${llvm_root}bolt/lib/Utils",
    "//${llvm_root}llvm/lib/CodeGen/AsmPrinter",
    "//${llvm_root}llvm/lib/DWARFLinker",
    "//${llvm_root}llvm/lib/DWP",
    "//${llvm_root}llvm/lib/DebugInfo/DWARF",
    "//${llvm_root}llvm/lib/ExecutionEngine/JITLink",
    "//${llvm_root}llvm/lib/MC",
    "//${llvm_root}llvm/lib/Object",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/TargetParser",
    "//${llvm_build_root}build/libs/pthread",
  ]
  sources = [
    "BinaryPassManager.cpp",
    "BoltDiff.cpp",
    "DWARFRewriter.cpp",
    "ExecutableFileMemoryManager.cpp",
    "JITLinkLinker.cpp",
    "LinuxKernelRewriter.cpp",
    "MachORewriteInstance.cpp",
    "MetadataManager.cpp",
    "PseudoProbeRewriter.cpp",
    "RewriteInstance.cpp",
    "SDTRewriter.cpp",
  ]

  defines = []
  if (llvm_build_AArch64) {
    defines += [ "AARCH64_AVAILABLE" ]
    deps += [ "//${llvm_root}bolt/lib/Target/AArch64" ]
  }
  if (llvm_build_X86) {
    defines += [ "X86_AVAILABLE" ]
    deps += [ "//${llvm_root}bolt/lib/Target/X86" ]
  }
}
