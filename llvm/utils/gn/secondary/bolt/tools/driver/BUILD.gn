import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/build/symlink_or_copy.gni")

symlinks = [
  "perf2bolt",
  "llvm-boltdiff",
]
foreach(symlink, symlinks) {
  symlink_or_copy(symlink) {
    deps = [ ":llvm-bolt" ]
    source = "llvm-bolt"
    output = "$root_out_dir/bin/$symlink"
  }
}

# //:llvm-bolt depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-bolt" ]
  foreach(symlink, symlinks) {
    deps += [ ":$symlink" ]
  }
}

executable("llvm-bolt") {
  configs += [ "//${llvm_build_root}/build:bolt_code" ]
  deps = [
    "//${llvm_build_root}/secondary/bolt/lib/Profile",
    "//${llvm_build_root}/secondary/bolt/lib/Rewrite",
    "//${llvm_build_root}/secondary/bolt/lib/Utils",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Object",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target:TargetsToBuild",
  ]

  # FIXME: Add dependency on //${llvm_build_root}/secondary/bolt/runtime once that exists.

  sources = [ "llvm-bolt.cpp" ]
}
