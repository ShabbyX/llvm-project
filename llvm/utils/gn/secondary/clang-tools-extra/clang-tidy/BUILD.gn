import("//build_overrides/llvm.gni")

import("//${llvm_root}clang/lib/StaticAnalyzer/Frontend/enable.gni")
import("//${llvm_build_root}build/write_cmake_config.gni")
import("enable.gni")

config("clang-tidy-config_Config") {
  visibility = [ ":clang-tidy-config" ]
  include_dirs = [ "$target_gen_dir" ]
}

write_cmake_config("clang-tidy-config") {
  input = "clang-tidy-config.h.cmake"
  output = "$target_gen_dir/clang-tidy-config.h"
  values = []

  if (clang_tidy_enable_static_analyzer) {
    values += [ "CLANG_TIDY_ENABLE_STATIC_ANALYZER=1" ]
  } else {
    values += [ "CLANG_TIDY_ENABLE_STATIC_ANALYZER=" ]
  }

  # Let targets depending on this find the generated file.
  public_configs = [ ":clang-tidy-config_Config" ]
}

static_library("clang-tidy") {
  output_name = "clangTidy"
  configs += [ "//${llvm_build_root}build:clang_code" ]
  deps = [
    ":clang-tidy-config",
    "//${llvm_root}clang/include/clang/StaticAnalyzer/Checkers",
    "//${llvm_root}clang/lib/AST",
    "//${llvm_root}clang/lib/ASTMatchers",
    "//${llvm_root}clang/lib/Basic",
    "//${llvm_root}clang/lib/Format",
    "//${llvm_root}clang/lib/Frontend",
    "//${llvm_root}clang/lib/Lex",
    "//${llvm_root}clang/lib/Rewrite",
    "//${llvm_root}clang/lib/Sema",
    "//${llvm_root}clang/lib/Serialization",
    "//${llvm_root}clang/lib/Tooling",
    "//${llvm_root}clang/lib/Tooling/Core",
    "//${llvm_root}llvm/lib/Support",
  ]

  if (clang_tidy_enable_static_analyzer) {
    deps += [
      "//${llvm_root}clang/lib/StaticAnalyzer/Core",
      "//${llvm_root}clang/lib/StaticAnalyzer/Frontend",
    ]
  }

  sources = [
    "ClangTidy.cpp",
    "ClangTidyCheck.cpp",
    "ClangTidyDiagnosticConsumer.cpp",
    "ClangTidyModule.cpp",
    "ClangTidyOptions.cpp",
    "ClangTidyProfiling.cpp",
    "ExpandModularHeadersPPCallbacks.cpp",
    "GlobList.cpp",
    "NoLintDirectiveHandler.cpp",
  ]
}

group("all-checks") {
  # If you add a check, also add it to ClangTidyForceLinker.h.
  deps = [
    "//${llvm_root}clang-tools-extra/clang-tidy/abseil",
    "//${llvm_root}clang-tools-extra/clang-tidy/altera",
    "//${llvm_root}clang-tools-extra/clang-tidy/android",
    "//${llvm_root}clang-tools-extra/clang-tidy/boost",
    "//${llvm_root}clang-tools-extra/clang-tidy/bugprone",
    "//${llvm_root}clang-tools-extra/clang-tidy/cert",
    "//${llvm_root}clang-tools-extra/clang-tidy/concurrency",
    "//${llvm_root}clang-tools-extra/clang-tidy/cppcoreguidelines",
    "//${llvm_root}clang-tools-extra/clang-tidy/darwin",
    "//${llvm_root}clang-tools-extra/clang-tidy/fuchsia",
    "//${llvm_root}clang-tools-extra/clang-tidy/google",
    "//${llvm_root}clang-tools-extra/clang-tidy/hicpp",
    "//${llvm_root}clang-tools-extra/clang-tidy/linuxkernel",
    "//${llvm_root}clang-tools-extra/clang-tidy/llvm",
    "//${llvm_root}clang-tools-extra/clang-tidy/llvmlibc",
    "//${llvm_root}clang-tools-extra/clang-tidy/misc",
    "//${llvm_root}clang-tools-extra/clang-tidy/modernize",
    "//${llvm_root}clang-tools-extra/clang-tidy/objc",
    "//${llvm_root}clang-tools-extra/clang-tidy/openmp",
    "//${llvm_root}clang-tools-extra/clang-tidy/performance",
    "//${llvm_root}clang-tools-extra/clang-tidy/portability",
    "//${llvm_root}clang-tools-extra/clang-tidy/readability",
    "//${llvm_root}clang-tools-extra/clang-tidy/zircon",
  ]
  if (clang_tidy_enable_static_analyzer) {
    deps += [ "//${llvm_root}clang-tools-extra/clang-tidy/mpi" ]
  }
}
