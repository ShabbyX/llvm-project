import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/clang/lib/ARCMigrate/enable.gni")
import("//${llvm_build_root}/secondary/clang/lib/StaticAnalyzer/Frontend/enable.gni")

assert(clang_enable_static_analyzer || !clang_enable_arcmt,
       "Cannot disable static analyzer while enabling ARCMT")

static_library("FrontendTool") {
  output_name = "clangFrontendTool"
  configs += [ "//${llvm_build_root}/build:clang_code" ]
  configs += llvm_code_override_configs_append
  configs -= llvm_code_override_configs_remove
  deps = [
    "//${llvm_build_root}/secondary/clang/include/clang/Config",
    "//${llvm_build_root}/secondary/clang/lib/Basic",
    "//${llvm_build_root}/secondary/clang/lib/CodeGen",
    "//${llvm_build_root}/secondary/clang/lib/Driver",
    "//${llvm_build_root}/secondary/clang/lib/ExtractAPI",
    "//${llvm_build_root}/secondary/clang/lib/Frontend",
    "//${llvm_build_root}/secondary/clang/lib/Frontend/Rewrite",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
  ]
  if (clang_enable_arcmt) {
    deps += [ "//${llvm_build_root}/secondary/clang/lib/ARCMigrate" ]
  }
  if (clang_enable_static_analyzer) {
    deps += [ "//${llvm_build_root}/secondary/clang/lib/StaticAnalyzer/Frontend" ]
  }
  sources = [ "ExecuteCompilerInvocation.cpp" ]
}
