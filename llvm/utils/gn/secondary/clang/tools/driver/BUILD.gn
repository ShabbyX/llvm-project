import("//build_overrides/llvm.gni")

import("//${llvm_build_root}build/driver_executable.gni")
import("//${llvm_build_root}build/symlink_or_copy.gni")
import("//${llvm_build_root}build/write_cmake_config.gni")
import("//${llvm_root}llvm/version.gni")

symlinks = [
  # target_name, symlink_target pairs: GN doesn't support '+' in rule names.
  [
    "clangxx",
    "clang++",
  ],
  [
    "clang-cl",
    "clang-cl",
  ],
  [
    "clang-cpp",
    "clang-cpp",
  ],
]
foreach(target, symlinks) {
  symlink_or_copy(target[0]) {
    deps = [ ":clang" ]
    source = "clang"
    output = "$root_out_dir/bin/${target[1]}"
  }
}

# //:clang depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":clang" ]
  foreach(target, symlinks) {
    deps += [ ":${target[0]}" ]
  }
}

if (host_os == "mac") {
  write_cmake_config("write_info_plist") {
    input = "Info.plist.in"
    output = "$target_gen_dir/Info.plist"
    values = [
      "TOOL_INFO_BUILD_VERSION=$llvm_version_major.$llvm_version_minor",
      "TOOL_INFO_NAME=clang",
      "TOOL_INFO_UTI=org.llvm.clang",
      "TOOL_INFO_VERSION=$llvm_version",
    ]
  }
}

driver_executable("clang") {
  configs += [ "//${llvm_build_root}build:clang_code" ]
  deps = [
    "//${llvm_root}clang/include/clang/Config",
    "//${llvm_root}clang/lib/Basic",
    "//${llvm_root}clang/lib/CodeGen",
    "//${llvm_root}clang/lib/Driver",
    "//${llvm_root}clang/lib/Frontend",
    "//${llvm_root}clang/lib/FrontendTool",
    "//${llvm_root}clang/lib/Headers",
    "//${llvm_root}clang/tools/clang-linker-wrapper",
    "//${llvm_root}clang/tools/clang-offload-bundler",
    "//${llvm_root}clang/tools/clang-offload-packager",
    "//${llvm_root}llvm/include/llvm/Config:llvm-config",
    "//${llvm_root}llvm/lib/Analysis",
    "//${llvm_root}llvm/lib/CodeGen",
    "//${llvm_root}llvm/lib/IR",
    "//${llvm_root}llvm/lib/MC",
    "//${llvm_root}llvm/lib/MC/MCParser",
    "//${llvm_root}llvm/lib/Option",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/Target:TargetsToBuild",
    "//${llvm_root}llvm/lib/TargetParser",
    "//${llvm_root}llvm/lib/Transforms/IPO",
    "//${llvm_root}llvm/lib/Transforms/InstCombine",
    "//${llvm_root}llvm/lib/Transforms/Instrumentation",
    "//${llvm_root}llvm/lib/Transforms/ObjCARC",
    "//${llvm_root}llvm/lib/Transforms/Scalar",
    "//${llvm_root}llvm/lib/Transforms/Utils",
    "//${llvm_root}llvm/lib/Transforms/Vectorize",
  ]
  assert_no_deps = [ "//${llvm_root}llvm/lib/Debuginfod" ]
  if (host_os == "mac") {
    deps += [ ":write_info_plist" ]
    plist = get_target_outputs(":write_info_plist")
    ldflags = [ "-Wl,-sectcreate,__TEXT,__info_plist," +
                rebase_path(plist[0], root_out_dir) ]

    # On macOS, libcxx headers are bundled with the compiler, not with the
    # sysroot. As a convenience, automatically copy them over when buildling
    # clang.  This is different from the CMake build, which requires devs to
    # explicitly build the "libcxx" target (which also needlessly compiles the
    # libcxx sources) to get a working compiler.
    deps += [ "//${llvm_root}libcxx/include:copy_headers" ]
  }
  sources = [
    "cc1_main.cpp",
    "cc1as_main.cpp",
    "cc1gen_reproducer_main.cpp",
    "driver.cpp",
  ]
}
