import("//build_overrides/llvm.gni")

group("lib") {
  deps = [
    "//${llvm_root}compiler-rt/lib/asan:ignorelist($host_toolchain)",
    "//${llvm_root}compiler-rt/lib/cfi:ignorelist($host_toolchain)",
  ]
  if (current_os == "linux") {
    deps += [ "//${llvm_root}compiler-rt/lib/msan" ]
  }
  if (current_os == "linux" || current_os == "android") {
    deps += [ "//${llvm_root}compiler-rt/lib/ubsan_minimal" ]
  }
  if (current_os != "win" && current_os != "baremetal") {
    deps += [
      "//${llvm_root}compiler-rt/lib/asan",
      "//${llvm_root}compiler-rt/lib/ubsan",
    ]
    if (current_cpu == "x64" || current_cpu == "arm64") {
      deps += [ "//${llvm_root}compiler-rt/lib/tsan/rtl" ]
    }
  }
  if (current_os != "win") {
    deps += [ "//${llvm_root}compiler-rt/lib/builtins" ]
  }
  if (current_os != "baremetal") {
    deps += [ "//${llvm_root}compiler-rt/lib/profile" ]
  }
}
