import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/build/write_vcsrevision.gni")

config("write_vcsrevision_config") {
  # To pick up the generated inc file.
  include_dirs = [ target_gen_dir ]
  visibility = [ ":write_vcsversion" ]
}

write_vcsrevision("write_vcsversion") {
  visibility = [ ":Common" ]
  header = "$target_gen_dir/VCSVersion.inc"
  names = [ "LLD" ]
  public_configs = [ ":write_vcsrevision_config" ]
}

static_library("Common") {
  output_name = "lldCommon"
  configs += [ "//${llvm_build_root}/build:lld_code" ]
  public_deps = [
    # public_dep because public header Version.h includes generated Version.inc.
    "//${llvm_build_root}/secondary/lld/include/lld/Common:version",
  ]
  deps = [
    ":write_vcsversion",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen",
    "//${llvm_build_root}/secondary/llvm/lib/Demangle",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]
  sources = [
    "Args.cpp",
    "CommonLinkerContext.cpp",
    "DWARF.cpp",
    "DriverDispatcher.cpp",
    "ErrorHandler.cpp",
    "Filesystem.cpp",
    "Memory.cpp",
    "Reproduce.cpp",
    "Strings.cpp",
    "TargetOptionsCommandFlags.cpp",
    "Timer.cpp",
    "Version.cpp",
  ]
}
