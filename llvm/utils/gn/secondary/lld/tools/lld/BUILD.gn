import("//build_overrides/llvm.gni")

import("//${llvm_build_root}build/driver_executable.gni")
import("//${llvm_build_root}build/symlink_or_copy.gni")

symlinks = [
  "lld-link",
  "ld.lld",
  "ld64.lld",
  "wasm-ld",
]
foreach(target, symlinks) {
  symlink_or_copy(target) {
    deps = [ ":lld" ]
    source = "lld"
    output = "$root_out_dir/bin/$target"
  }
}

# //:lld depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":lld" ]
  foreach(target, symlinks) {
    deps += [ ":$target" ]
  }
}

driver_executable("lld") {
  configs += [ "//${llvm_build_root}build:lld_code" ]
  deps = [
    "//${llvm_root}lld/COFF",
    "//${llvm_root}lld/ELF",
    "//${llvm_root}lld/MachO:MachO",
    "//${llvm_root}lld/MinGW",
    "//${llvm_root}lld/wasm",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/TargetParser",
    "//${llvm_build_root}build/libs/xar",
  ]
  assert_no_deps = [ "//${llvm_root}llvm/lib/Debuginfod" ]
  sources = [ "lld.cpp" ]
}
