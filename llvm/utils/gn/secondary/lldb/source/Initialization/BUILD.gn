import("//build_overrides/llvm.gni")

static_library("Initialization") {
  output_name = "lldbInitialization"
  configs += [ "//${llvm_build_root}build:lldb_code" ]
  deps = [
    "//${llvm_root}lldb/source/Core",
    "//${llvm_root}lldb/source/Host",
    "//${llvm_root}lldb/source/Plugins/Process/gdb-remote",
    "//${llvm_root}llvm/lib/Support",
  ]

  if (current_os == "linux" || current_os == "android") {
    deps += [ "//${llvm_root}lldb/source/Plugins/Process/POSIX" ]
  } else if (current_os == "win") {
    deps += [ "//${llvm_root}lldb/source/Plugins/Process/Windows/Common" ]
  }

  # SystemInitializerCommon.cpp includes headers from Plugins/Process/....
  include_dirs = [ ".." ]
  sources = [
    "SystemInitializer.cpp",
    "SystemInitializerCommon.cpp",
    "SystemLifetimeManager.cpp",
  ]
}
