import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/lldb/utils/TableGen/lldb_tablegen.gni")

lldb_tablegen("ObjectFilePECOFFProperties") {
  args = [ "-gen-lldb-property-defs" ]
}

lldb_tablegen("ObjectFilePECOFFPropertiesEnum") {
  args = [ "-gen-lldb-property-enum-defs" ]
  td_file = "ObjectFilePECOFFProperties.td"
}

static_library("PECOFF") {
  output_name = "lldbPluginObjectFilePECOFF"
  configs += [ "//${llvm_build_root}/build:lldb_code" ]
  deps = [
    ":ObjectFilePECOFFProperties",
    ":ObjectFilePECOFFPropertiesEnum",
    "//${llvm_build_root}/secondary/lldb/source/Core",
    "//${llvm_build_root}/secondary/lldb/source/Host",
    "//${llvm_build_root}/secondary/lldb/source/Symbol",
    "//${llvm_build_root}/secondary/lldb/source/Target",
    "//${llvm_build_root}/secondary/llvm/lib/BinaryFormat",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]

  # Reaches into Plugins/Process/Utility.
  include_dirs = [ "//${llvm_build_root}/secondary/lldb/source" ]
  sources = [
    "ObjectFilePECOFF.cpp",
    "PECallFrameInfo.cpp",
    "WindowsMiniDump.cpp",
  ]

  if (current_os == "win") {
    # Dbghelp is used on windows for writing minidump files.
    libs = [ "dbghelp.lib" ]
  }
}
