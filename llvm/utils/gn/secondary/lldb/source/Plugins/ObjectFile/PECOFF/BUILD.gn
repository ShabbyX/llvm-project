import("//build_overrides/llvm.gni")

import("//${llvm_root}lldb/utils/TableGen/lldb_tablegen.gni")

lldb_tablegen("ObjectFilePECOFFProperties") {
  args = [ "-gen-lldb-property-defs" ]
}

lldb_tablegen("ObjectFilePECOFFPropertiesEnum") {
  args = [ "-gen-lldb-property-enum-defs" ]
  td_file = "ObjectFilePECOFFProperties.td"
}

static_library("PECOFF") {
  output_name = "lldbPluginObjectFilePECOFF"
  configs += [ "//${llvm_build_root}build:lldb_code" ]
  deps = [
    ":ObjectFilePECOFFProperties",
    ":ObjectFilePECOFFPropertiesEnum",
    "//${llvm_root}lldb/source/Core",
    "//${llvm_root}lldb/source/Host",
    "//${llvm_root}lldb/source/Symbol",
    "//${llvm_root}lldb/source/Target",
    "//${llvm_root}llvm/lib/BinaryFormat",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/TargetParser",
  ]

  # Reaches into Plugins/Process/Utility.
  include_dirs = [ "//${llvm_root}lldb/source" ]
  sources = [
    "ObjectFilePECOFF.cpp",
    "PECallFrameInfo.cpp",
    "WindowsMiniDump.cpp",
  ]

  if (current_os == "win") {
    # Dbghelp is used on windows for writing minidump files.
    libs = [ "dbghelp.lib" ]
  }
}
