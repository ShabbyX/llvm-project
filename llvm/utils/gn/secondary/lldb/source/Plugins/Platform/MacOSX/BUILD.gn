import("//build_overrides/llvm.gni")

import("//${llvm_root}lldb/utils/TableGen/lldb_tablegen.gni")

lldb_tablegen("PlatformMacOSXProperties") {
  args = [ "-gen-lldb-property-defs" ]
}

lldb_tablegen("PlatformMacOSXPropertiesEnum") {
  args = [ "-gen-lldb-property-enum-defs" ]
  td_file = "PlatformMacOSXProperties.td"
}

static_library("MacOSX") {
  output_name = "lldbPluginPlatformMacOSX"
  configs += [
    "//${llvm_build_root}build:clang_code",
    "//${llvm_build_root}build:lldb_code",
  ]
  deps = [
    ":PlatformMacOSXProperties",
    ":PlatformMacOSXPropertiesEnum",
    "//${llvm_root}clang/lib/Basic",
    "//${llvm_root}lldb/source/Breakpoint",
    "//${llvm_root}lldb/source/Core",
    "//${llvm_root}lldb/source/Host",
    "//${llvm_root}lldb/source/Interpreter",
    "//${llvm_root}lldb/source/Plugins/DynamicLoader/Darwin-Kernel",
    "//${llvm_root}lldb/source/Plugins/ObjectContainer/Mach-O-Fileset",
    "//${llvm_root}lldb/source/Plugins/Platform/POSIX",
    "//${llvm_root}lldb/source/Symbol",
    "//${llvm_root}lldb/source/Target",
    "//${llvm_root}lldb/source/Utility",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/TargetParser",
  ]

  # Reaches into Plugins/Platform/POSIX.
  include_dirs = [ "//${llvm_root}lldb/source" ]
  sources = [
    "PlatformDarwin.cpp",
    "PlatformDarwinDevice.cpp",
    "PlatformDarwinKernel.cpp",
    "PlatformMacOSX.cpp",
    "PlatformRemoteAppleBridge.cpp",
    "PlatformRemoteAppleTV.cpp",
    "PlatformRemoteAppleWatch.cpp",
    "PlatformRemoteDarwinDevice.cpp",
    "PlatformRemoteMacOSX.cpp",
    "PlatformRemoteiOS.cpp",
  ]
  if (host_os == "mac") {
    deps += [ "objcxx" ]
    sources += [ "PlatformAppleSimulator.cpp" ]
  }
}
