import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/lldb/utils/TableGen/lldb_tablegen.gni")

lldb_tablegen("PlatformMacOSXProperties") {
  args = [ "-gen-lldb-property-defs" ]
}

lldb_tablegen("PlatformMacOSXPropertiesEnum") {
  args = [ "-gen-lldb-property-enum-defs" ]
  td_file = "PlatformMacOSXProperties.td"
}

static_library("MacOSX") {
  output_name = "lldbPluginPlatformMacOSX"
  configs += [
    "//${llvm_build_root}/build:clang_code",
    "//${llvm_build_root}/build:lldb_code",
  ]
  deps = [
    ":PlatformMacOSXProperties",
    ":PlatformMacOSXPropertiesEnum",
    "//${llvm_build_root}/secondary/clang/lib/Basic",
    "//${llvm_build_root}/secondary/lldb/source/Breakpoint",
    "//${llvm_build_root}/secondary/lldb/source/Core",
    "//${llvm_build_root}/secondary/lldb/source/Host",
    "//${llvm_build_root}/secondary/lldb/source/Interpreter",
    "//${llvm_build_root}/secondary/lldb/source/Plugins/DynamicLoader/Darwin-Kernel",
    "//${llvm_build_root}/secondary/lldb/source/Plugins/ObjectContainer/Mach-O-Fileset",
    "//${llvm_build_root}/secondary/lldb/source/Plugins/Platform/POSIX",
    "//${llvm_build_root}/secondary/lldb/source/Symbol",
    "//${llvm_build_root}/secondary/lldb/source/Target",
    "//${llvm_build_root}/secondary/lldb/source/Utility",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]

  # Reaches into Plugins/Platform/POSIX.
  include_dirs = [ "//${llvm_build_root}/secondary/lldb/source" ]
  sources = [
    "PlatformDarwin.cpp",
    "PlatformDarwinDevice.cpp",
    "PlatformDarwinKernel.cpp",
    "PlatformMacOSX.cpp",
    "PlatformRemoteAppleBridge.cpp",
    "PlatformRemoteAppleTV.cpp",
    "PlatformRemoteAppleWatch.cpp",
    "PlatformRemoteDarwinDevice.cpp",
    "PlatformRemoteMacOSX.cpp",
    "PlatformRemoteiOS.cpp",
  ]
  if (host_os == "mac") {
    deps += [ "objcxx" ]
    sources += [ "PlatformAppleSimulator.cpp" ]
  }
}
