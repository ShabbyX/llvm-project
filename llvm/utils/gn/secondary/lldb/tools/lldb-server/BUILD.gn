import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/compiler-rt/target.gni")
import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

tablegen("LLGSOptions") {
  visibility = [ ":lldb-server" ]
  args = [ "-gen-opt-parser-defs" ]
}

executable("lldb-server") {
  configs += [
    "//${llvm_build_root}/build:clang_code",
    "//${llvm_build_root}/build:lldb_code",
  ]
  deps = [
    ":LLGSOptions",

    #"//${llvm_build_root}/secondary/lldb/include/lldb/Host:Config",
    "//${llvm_build_root}/secondary/lldb/source/Host",
    "//${llvm_build_root}/secondary/lldb/source/Initialization",
    "//${llvm_build_root}/secondary/lldb/source/Plugins/Instruction/ARM",
    "//${llvm_build_root}/secondary/lldb/source/Version",

    #"//${llvm_build_root}/secondary/lldb/source/Plugins/Instruction/MIPS", # XXX
    #"//${llvm_build_root}/secondary/lldb/source/Plugins/Instruction/MIPS64", # XXX
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",

    # Dep of //${llvm_build_root}/secondary/lldb/source/Core, but omitted there due to a
    # dependency cyle. Need to add dep here.
    "//${llvm_build_root}/secondary/lldb/source/Plugins/Language/CPlusPlus",
  ]

  if (current_os == "android" || current_os == "linux") {
    deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/Process/Linux" ]
    if (current_os == "linux") {
      deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/Platform/Linux" ]
    } else {
      # XXX
      #deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/Platform/Android" ]
    }
  }

  #} else if (current_os == "freebsd") {
  #deps += [ "//${llvm_build_root}/secondary/lldb/source/Platform/Process/FreeBSD" ]
  #deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/Process/FreeBSD" ]
  #} else if (current_os == "netbsd") {
  #deps += [ "//${llvm_build_root}/secondary/lldb/source/Platform/Process/NetBSD" ]
  #deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/Process/NetBSD" ]

  if (current_os == "mac") {
    deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/ObjectFile/Mach-O" ]
    deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/Platform/MacOSX" ]
  } else if (current_os == "win") {
    deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/ObjectFile/PECOFF" ]
    deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/Platform/Windows" ]
  } else {
    deps += [ "//${llvm_build_root}/secondary/lldb/source/Plugins/ObjectFile/ELF" ]
  }

  # FIXME: codesign stuff on macos; default to "lldb_codesign"

  # Reaches into Plugins/ObjectFile/Mach-O.
  include_dirs = [ "//${llvm_build_root}/secondary/lldb/source" ]
  sources = [
    "Acceptor.cpp",
    "LLDBServerUtilities.cpp",
    "SystemInitializerLLGS.cpp",
    "lldb-gdbserver.cpp",
    "lldb-platform.cpp",
    "lldb-server.cpp",
  ]

  if (current_os == "android") {
    output_dir = "$root_build_dir/runtimes_ndk_cxx/$crt_current_target_arch"
  }
}
