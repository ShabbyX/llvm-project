import("//build_overrides/llvm.gni")

import("//${llvm_root}compiler-rt/target.gni")
import("//${llvm_root}llvm/utils/TableGen/tablegen.gni")

tablegen("LLGSOptions") {
  visibility = [ ":lldb-server" ]
  args = [ "-gen-opt-parser-defs" ]
}

executable("lldb-server") {
  configs += [
    "//${llvm_build_root}build:clang_code",
    "//${llvm_build_root}build:lldb_code",
  ]
  deps = [
    ":LLGSOptions",

    #"//${llvm_root}lldb/include/lldb/Host:Config",
    "//${llvm_root}lldb/source/Host",
    "//${llvm_root}lldb/source/Initialization",
    "//${llvm_root}lldb/source/Plugins/Instruction/ARM",
    "//${llvm_root}lldb/source/Version",

    #"//${llvm_root}lldb/source/Plugins/Instruction/MIPS", # XXX
    #"//${llvm_root}lldb/source/Plugins/Instruction/MIPS64", # XXX
    "//${llvm_root}llvm/lib/Option",
    "//${llvm_root}llvm/lib/Support",

    # Dep of //${llvm_root}lldb/source/Core, but omitted there due to a
    # dependency cyle. Need to add dep here.
    "//${llvm_root}lldb/source/Plugins/Language/CPlusPlus",
  ]

  if (current_os == "android" || current_os == "linux") {
    deps += [ "//${llvm_root}lldb/source/Plugins/Process/Linux" ]
    if (current_os == "linux") {
      deps += [ "//${llvm_root}lldb/source/Plugins/Platform/Linux" ]
    } else {
      # XXX
      #deps += [ "//${llvm_root}lldb/source/Plugins/Platform/Android" ]
    }
  }

  #} else if (current_os == "freebsd") {
  #deps += [ "//${llvm_root}lldb/source/Platform/Process/FreeBSD" ]
  #deps += [ "//${llvm_root}lldb/source/Plugins/Process/FreeBSD" ]
  #} else if (current_os == "netbsd") {
  #deps += [ "//${llvm_root}lldb/source/Platform/Process/NetBSD" ]
  #deps += [ "//${llvm_root}lldb/source/Plugins/Process/NetBSD" ]

  if (current_os == "mac") {
    deps += [ "//${llvm_root}lldb/source/Plugins/ObjectFile/Mach-O" ]
    deps += [ "//${llvm_root}lldb/source/Plugins/Platform/MacOSX" ]
  } else if (current_os == "win") {
    deps += [ "//${llvm_root}lldb/source/Plugins/ObjectFile/PECOFF" ]
    deps += [ "//${llvm_root}lldb/source/Plugins/Platform/Windows" ]
  } else {
    deps += [ "//${llvm_root}lldb/source/Plugins/ObjectFile/ELF" ]
  }

  # FIXME: codesign stuff on macos; default to "lldb_codesign"

  # Reaches into Plugins/ObjectFile/Mach-O.
  include_dirs = [ "//${llvm_root}lldb/source" ]
  sources = [
    "Acceptor.cpp",
    "LLDBServerUtilities.cpp",
    "SystemInitializerLLGS.cpp",
    "lldb-gdbserver.cpp",
    "lldb-platform.cpp",
    "lldb-server.cpp",
  ]

  if (current_os == "android") {
    output_dir = "$root_build_dir/runtimes_ndk_cxx/$crt_current_target_arch"
  }
}
