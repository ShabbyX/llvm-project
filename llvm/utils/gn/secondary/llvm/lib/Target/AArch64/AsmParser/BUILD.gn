import("//build_overrides/llvm.gni")

import("//${llvm_root}llvm/utils/TableGen/tablegen.gni")

tablegen("AArch64GenAsmMatcher") {
  visibility = [
    ":AsmParser",
    "//${llvm_root}llvm/lib/Target/AArch64:LLVMAArch64CodeGen",
  ]
  args = [ "-gen-asm-matcher" ]
  td_file = "../AArch64.td"
}

static_library("AsmParser") {
  output_name = "LLVMAArch64AsmParser"
  deps = [
    ":AArch64GenAsmMatcher",
    "//${llvm_root}llvm/lib/CodeGen",
    "//${llvm_root}llvm/lib/MC",
    "//${llvm_root}llvm/lib/MC/MCParser",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/Target/AArch64/MCTargetDesc",
    "//${llvm_root}llvm/lib/Target/AArch64/TargetInfo",
    "//${llvm_root}llvm/lib/Target/AArch64/Utils",
    "//${llvm_root}llvm/lib/TargetParser",
  ]
  include_dirs = [ ".." ]
  sources = [ "AArch64AsmParser.cpp" ]
}
