import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

tablegen("ARMGenCallingConv") {
  visibility = [ ":LLVMARMCodeGen" ]
  args = [ "-gen-callingconv" ]
  td_file = "ARM.td"
}

tablegen("ARMGenDAGISel") {
  visibility = [ ":LLVMARMCodeGen" ]
  args = [ "-gen-dag-isel" ]
  td_file = "ARM.td"
}

tablegen("ARMGenFastISel") {
  visibility = [ ":LLVMARMCodeGen" ]
  args = [ "-gen-fast-isel" ]
  td_file = "ARM.td"
}

tablegen("ARMGenGlobalISel") {
  visibility = [ ":LLVMARMCodeGen" ]
  args = [ "-gen-global-isel" ]
  td_file = "ARM.td"
}

tablegen("ARMGenMCPseudoLowering") {
  visibility = [ ":LLVMARMCodeGen" ]
  args = [ "-gen-pseudo-lowering" ]
  td_file = "ARM.td"
}

tablegen("ARMGenRegisterBank") {
  visibility = [ ":LLVMARMCodeGen" ]
  args = [ "-gen-register-bank" ]
  td_file = "ARM.td"
}

static_library("LLVMARMCodeGen") {
  deps = [
    ":ARMGenCallingConv",
    ":ARMGenDAGISel",
    ":ARMGenFastISel",
    ":ARMGenGlobalISel",
    ":ARMGenMCPseudoLowering",
    ":ARMGenRegisterBank",
    "MCTargetDesc",
    "TargetInfo",
    "Utils",
    "//${llvm_build_root}/secondary/llvm/include/llvm/Config:llvm-config",
    "//${llvm_build_root}/secondary/llvm/lib/Analysis",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/AsmPrinter",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/GlobalISel",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/SelectionDAG",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
    "//${llvm_build_root}/secondary/llvm/lib/Transforms/CFGuard",
    "//${llvm_build_root}/secondary/llvm/lib/Transforms/IPO",
    "//${llvm_build_root}/secondary/llvm/lib/Transforms/Utils",
  ]
  include_dirs = [ "." ]
  sources = [
    "A15SDOptimizer.cpp",
    "ARMAsmPrinter.cpp",
    "ARMBaseInstrInfo.cpp",
    "ARMBaseRegisterInfo.cpp",
    "ARMBasicBlockInfo.cpp",
    "ARMBlockPlacement.cpp",
    "ARMBranchTargets.cpp",
    "ARMCallLowering.cpp",
    "ARMCallingConv.cpp",
    "ARMConstantIslandPass.cpp",
    "ARMConstantPoolValue.cpp",
    "ARMExpandPseudoInsts.cpp",
    "ARMFastISel.cpp",
    "ARMFixCortexA57AES1742098Pass.cpp",
    "ARMFrameLowering.cpp",
    "ARMHazardRecognizer.cpp",
    "ARMISelDAGToDAG.cpp",
    "ARMISelLowering.cpp",
    "ARMInstrInfo.cpp",
    "ARMInstructionSelector.cpp",
    "ARMLegalizerInfo.cpp",
    "ARMLoadStoreOptimizer.cpp",
    "ARMLowOverheadLoops.cpp",
    "ARMMCInstLower.cpp",
    "ARMMachineFunctionInfo.cpp",
    "ARMMacroFusion.cpp",
    "ARMOptimizeBarriersPass.cpp",
    "ARMParallelDSP.cpp",
    "ARMRegisterBankInfo.cpp",
    "ARMRegisterInfo.cpp",
    "ARMSLSHardening.cpp",
    "ARMSelectionDAGInfo.cpp",
    "ARMSubtarget.cpp",
    "ARMTargetMachine.cpp",
    "ARMTargetObjectFile.cpp",
    "ARMTargetTransformInfo.cpp",
    "MLxExpansionPass.cpp",
    "MVEGatherScatterLowering.cpp",
    "MVELaneInterleavingPass.cpp",
    "MVETPAndVPTOptimisationsPass.cpp",
    "MVETailPredication.cpp",
    "MVEVPTBlockPass.cpp",
    "Thumb1FrameLowering.cpp",
    "Thumb1InstrInfo.cpp",
    "Thumb2ITBlockPass.cpp",
    "Thumb2InstrInfo.cpp",
    "Thumb2SizeReduction.cpp",
    "ThumbRegisterInfo.cpp",
  ]
  configs += llvm_code_override_configs_append
  configs -= llvm_code_override_configs_remove
}

# This is a bit different from most build files: Due to this group
# having the directory's name, "//${llvm_build_root}/secondary/llvm/lib/Target/ARM" will refer to this
# target, which pulls in the code in this directory *and all subdirectories*.
# For most other directories, "//${llvm_build_root}/secondary/llvm/lib/Foo" only pulls in the code directly
# in "llvm/lib/Foo". The forwarding targets in //${llvm_build_root}/secondary/llvm/lib/Target expect this
# different behavior.
group("ARM") {
  deps = [
    ":LLVMARMCodeGen",
    "AsmParser",
    "Disassembler",
    "MCTargetDesc",
    "TargetInfo",
    "Utils",
  ]
}
