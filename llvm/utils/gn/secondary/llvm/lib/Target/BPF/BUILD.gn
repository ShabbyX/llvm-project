import("//build_overrides/llvm.gni")

import("//${llvm_root}llvm/utils/TableGen/tablegen.gni")

tablegen("BPFGenCallingConv") {
  visibility = [ ":LLVMBPFCodeGen" ]
  args = [ "-gen-callingconv" ]
  td_file = "BPF.td"
}

tablegen("BPFGenDAGISel") {
  visibility = [ ":LLVMBPFCodeGen" ]
  args = [ "-gen-dag-isel" ]
  td_file = "BPF.td"
}

tablegen("BPFGenFastISel") {
  visibility = [ ":LLVMBPFCodeGen" ]
  args = [ "-gen-fast-isel" ]
  td_file = "BPF.td"
}

tablegen("BPFGenGlobalISel") {
  visibility = [ ":LLVMBPFCodeGen" ]
  args = [ "-gen-global-isel" ]
  td_file = "BPF.td"
}

tablegen("BPFGenMCPseudoLowering") {
  visibility = [ ":LLVMBPFCodeGen" ]
  args = [ "-gen-pseudo-lowering" ]
  td_file = "BPF.td"
}

tablegen("BPFGenRegisterBank") {
  visibility = [ ":LLVMBPFCodeGen" ]
  args = [ "-gen-register-bank" ]
  td_file = "BPF.td"
}

static_library("LLVMBPFCodeGen") {
  deps = [
    ":BPFGenCallingConv",
    ":BPFGenDAGISel",
    ":BPFGenFastISel",
    ":BPFGenGlobalISel",
    ":BPFGenMCPseudoLowering",
    ":BPFGenRegisterBank",
    "MCTargetDesc",
    "TargetInfo",
    "//${llvm_root}llvm/include/llvm/Config:llvm-config",
    "//${llvm_root}llvm/lib/Analysis",
    "//${llvm_root}llvm/lib/CodeGen",
    "//${llvm_root}llvm/lib/CodeGen/AsmPrinter",
    "//${llvm_root}llvm/lib/CodeGen/GlobalISel",
    "//${llvm_root}llvm/lib/CodeGen/SelectionDAG",
    "//${llvm_root}llvm/lib/IR",
    "//${llvm_root}llvm/lib/MC",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/Target",
    "//${llvm_root}llvm/lib/TargetParser",
  ]
  include_dirs = [ "." ]
  sources = [
    "BPFAbstractMemberAccess.cpp",
    "BPFAdjustOpt.cpp",
    "BPFAsmPrinter.cpp",
    "BPFCheckAndAdjustIR.cpp",
    "BPFFrameLowering.cpp",
    "BPFIRPeephole.cpp",
    "BPFISelDAGToDAG.cpp",
    "BPFISelLowering.cpp",
    "BPFInstrInfo.cpp",
    "BPFMCInstLower.cpp",
    "BPFMIChecking.cpp",
    "BPFMIPeephole.cpp",
    "BPFMISimplifyPatchable.cpp",
    "BPFPreserveDIType.cpp",
    "BPFRegisterInfo.cpp",
    "BPFSelectionDAGInfo.cpp",
    "BPFSubtarget.cpp",
    "BPFTargetMachine.cpp",
    "BTFDebug.cpp",
  ]
}

# This is a bit different from most build files: Due to this group
# having the directory's name, "//${llvm_root}llvm/lib/Target/BPF" will refer to this
# target, which pulls in the code in this directory *and all subdirectories*.
# For most other directories, "//${llvm_root}llvm/lib/Foo" only pulls in the code directly
# in "llvm/lib/Foo". The forwarding targets in //${llvm_root}llvm/lib/Target expect this
# different behavior.
group("BPF") {
  deps = [
    ":LLVMBPFCodeGen",
    "AsmParser",
    "Disassembler",
    "MCTargetDesc",
    "TargetInfo",
  ]
}
