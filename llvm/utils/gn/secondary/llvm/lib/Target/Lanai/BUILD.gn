import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

tablegen("LanaiGenCallingConv") {
  visibility = [ ":LLVMLanaiCodeGen" ]
  args = [ "-gen-callingconv" ]
  td_file = "Lanai.td"
}

tablegen("LanaiGenDAGISel") {
  visibility = [ ":LLVMLanaiCodeGen" ]
  args = [ "-gen-dag-isel" ]
  td_file = "Lanai.td"
}

static_library("LLVMLanaiCodeGen") {
  deps = [
    ":LanaiGenCallingConv",
    ":LanaiGenDAGISel",
    "MCTargetDesc",
    "TargetInfo",
    "//${llvm_build_root}/secondary/llvm/include/llvm/Config:llvm-config",
    "//${llvm_build_root}/secondary/llvm/lib/Analysis",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/AsmPrinter",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/SelectionDAG",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]
  include_dirs = [ "." ]
  sources = [
    "LanaiAsmPrinter.cpp",
    "LanaiDelaySlotFiller.cpp",
    "LanaiFrameLowering.cpp",
    "LanaiISelDAGToDAG.cpp",
    "LanaiISelLowering.cpp",
    "LanaiInstrInfo.cpp",
    "LanaiMCInstLower.cpp",
    "LanaiMachineFunctionInfo.cpp",
    "LanaiMemAluCombiner.cpp",
    "LanaiRegisterInfo.cpp",
    "LanaiSelectionDAGInfo.cpp",
    "LanaiSubtarget.cpp",
    "LanaiTargetMachine.cpp",
    "LanaiTargetObjectFile.cpp",
  ]
}

# This is a bit different from most build files: Due to this group
# having the directory's name, "//${llvm_build_root}/secondary/llvm/lib/Target/Lanai" will refer to this
# target, which pulls in the code in this directory *and all subdirectories*.
# For most other directories, "//${llvm_build_root}/secondary/llvm/lib/Foo" only pulls in the code directly
# in "llvm/lib/Foo". The forwarding targets in //${llvm_build_root}/secondary/llvm/lib/Target expect this
# different behavior.
group("Lanai") {
  deps = [
    ":LLVMLanaiCodeGen",
    "AsmParser",
    "Disassembler",
    "MCTargetDesc",
    "TargetInfo",
  ]
}
