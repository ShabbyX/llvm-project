import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

tablegen("LoongArchGenDAGISel") {
  visibility = [ ":LLVMLoongArchCodeGen" ]
  args = [ "-gen-dag-isel" ]
  td_file = "LoongArch.td"
}

tablegen("LoongArchGenMCPseudoLowering") {
  visibility = [ ":LLVMLoongArchCodeGen" ]
  args = [ "-gen-pseudo-lowering" ]
  td_file = "LoongArch.td"
}

static_library("LLVMLoongArchCodeGen") {
  deps = [
    ":LoongArchGenDAGISel",
    ":LoongArchGenMCPseudoLowering",

    # See https://reviews.llvm.org/D137532
    "AsmParser:LoongArchGenAsmMatcher",
    "MCTargetDesc",
    "TargetInfo",
    "//${llvm_build_root}/secondary/llvm/include/llvm/Config:llvm-config",
    "//${llvm_build_root}/secondary/llvm/lib/Analysis",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/AsmPrinter",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/SelectionDAG",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target",
  ]
  include_dirs = [ "." ]
  sources = [
    "LoongArchAsmPrinter.cpp",
    "LoongArchExpandAtomicPseudoInsts.cpp",
    "LoongArchExpandPseudoInsts.cpp",
    "LoongArchFrameLowering.cpp",
    "LoongArchISelDAGToDAG.cpp",
    "LoongArchISelLowering.cpp",
    "LoongArchInstrInfo.cpp",
    "LoongArchMCInstLower.cpp",
    "LoongArchRegisterInfo.cpp",
    "LoongArchSubtarget.cpp",
    "LoongArchTargetMachine.cpp",
    "LoongArchTargetTransformInfo.cpp",
  ]
}

# This is a bit different from most build files: Due to this group
# having the directory's name, "//${llvm_build_root}/secondary/llvm/lib/Target/LoongArch" will refer to this
# target, which pulls in the code in this directory *and all subdirectories*.
# For most other directories, "//${llvm_build_root}/secondary/llvm/lib/Foo" only pulls in the code directly
# in "llvm/lib/Foo". The forwarding targets in //${llvm_build_root}/secondary/llvm/lib/Target expect this
# different behavior.
group("LoongArch") {
  deps = [
    ":LLVMLoongArchCodeGen",
    "AsmParser",
    "Disassembler",
    "MCTargetDesc",
    "TargetInfo",
  ]
}
