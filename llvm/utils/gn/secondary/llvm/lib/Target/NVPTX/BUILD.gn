import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

tablegen("NVPTXGenDAGISel") {
  visibility = [ ":LLVMNVPTXCodeGen" ]
  args = [ "-gen-dag-isel" ]
  td_file = "NVPTX.td"
}

static_library("LLVMNVPTXCodeGen") {
  deps = [
    ":NVPTXGenDAGISel",
    "MCTargetDesc",
    "TargetInfo",
    "//${llvm_build_root}/secondary/llvm/include/llvm/Config:llvm-config",
    "//${llvm_build_root}/secondary/llvm/lib/Analysis",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/AsmPrinter",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/SelectionDAG",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
    "//${llvm_build_root}/secondary/llvm/lib/Transforms/IPO",
    "//${llvm_build_root}/secondary/llvm/lib/Transforms/Scalar",
    "//${llvm_build_root}/secondary/llvm/lib/Transforms/Utils",
    "//${llvm_build_root}/secondary/llvm/lib/Transforms/Vectorize",
  ]
  include_dirs = [ "." ]
  sources = [
    "NVPTXAliasAnalysis.cpp",
    "NVPTXAllocaHoisting.cpp",
    "NVPTXAsmPrinter.cpp",
    "NVPTXAssignValidGlobalNames.cpp",
    "NVPTXAtomicLower.cpp",
    "NVPTXCtorDtorLowering.cpp",
    "NVPTXFrameLowering.cpp",
    "NVPTXGenericToNVVM.cpp",
    "NVPTXISelDAGToDAG.cpp",
    "NVPTXISelLowering.cpp",
    "NVPTXImageOptimizer.cpp",
    "NVPTXInstrInfo.cpp",
    "NVPTXLowerAggrCopies.cpp",
    "NVPTXLowerAlloca.cpp",
    "NVPTXLowerArgs.cpp",
    "NVPTXLowerUnreachable.cpp",
    "NVPTXMCExpr.cpp",
    "NVPTXPeephole.cpp",
    "NVPTXPrologEpilogPass.cpp",
    "NVPTXProxyRegErasure.cpp",
    "NVPTXRegisterInfo.cpp",
    "NVPTXReplaceImageHandles.cpp",
    "NVPTXSubtarget.cpp",
    "NVPTXTargetMachine.cpp",
    "NVPTXTargetTransformInfo.cpp",
    "NVPTXUtilities.cpp",
    "NVVMIntrRange.cpp",
    "NVVMReflect.cpp",
  ]
}

# This is a bit different from most build files: Due to this group
# having the directory's name, "//${llvm_build_root}/secondary/llvm/lib/Target/NVPTX" will refer to this
# target, which pulls in the code in this directory *and all subdirectories*.
# For most other directories, "//${llvm_build_root}/secondary/llvm/lib/Foo" only pulls in the code directly
# in "llvm/lib/Foo". The forwarding targets in //${llvm_build_root}/secondary/llvm/lib/Target expect this
# different behavior.
group("NVPTX") {
  deps = [
    ":LLVMNVPTXCodeGen",
    "MCTargetDesc",
    "TargetInfo",
  ]
}
