import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

# RISCV is the only target that has a "compress instr emitter", and it's
# a bit strange in that it defines static functions depending on which
# defines are set. Instead of housing these functions in one library,
# various libraries include the generated .inc file with different defines set.
tablegen("RISCVGenCompressInstEmitter") {
  visibility = [
    ":LLVMRISCVCodeGen",
    "AsmParser",
    "MCTargetDesc",
  ]
  args = [ "-gen-compress-inst-emitter" ]
  td_file = "RISCV.td"
}

tablegen("RISCVGenDAGISel") {
  visibility = [ ":LLVMRISCVCodeGen" ]
  args = [ "-gen-dag-isel" ]
  td_file = "RISCV.td"
}

tablegen("RISCVGenGlobalISel") {
  visibility = [ ":LLVMRISCVCodeGen" ]
  args = [ "-gen-global-isel" ]
  td_file = "RISCV.td"
}

tablegen("RISCVGenMCPseudoLowering") {
  visibility = [ ":LLVMRISCVCodeGen" ]
  args = [ "-gen-pseudo-lowering" ]
  td_file = "RISCV.td"
}

tablegen("RISCVGenRegisterBank") {
  visibility = [ ":LLVMRISCVCodeGen" ]
  args = [ "-gen-register-bank" ]
  td_file = "RISCV.td"
}

static_library("LLVMRISCVCodeGen") {
  deps = [
    ":RISCVGenCompressInstEmitter",
    ":RISCVGenDAGISel",
    ":RISCVGenGlobalISel",
    ":RISCVGenMCPseudoLowering",
    ":RISCVGenRegisterBank",

    # See https://reviews.llvm.org/D69130
    "AsmParser:RISCVGenAsmMatcher",
    "MCTargetDesc",
    "TargetInfo",
    "//${llvm_build_root}/secondary/llvm/include/llvm/Config:llvm-config",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/AsmPrinter",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/GlobalISel",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/SelectionDAG",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
    "//${llvm_build_root}/secondary/llvm/lib/Transforms/IPO",
  ]
  include_dirs = [ "." ]
  sources = [
    "GISel/RISCVCallLowering.cpp",
    "GISel/RISCVInstructionSelector.cpp",
    "GISel/RISCVLegalizerInfo.cpp",
    "GISel/RISCVRegisterBankInfo.cpp",
    "RISCVAsmPrinter.cpp",
    "RISCVCodeGenPrepare.cpp",
    "RISCVExpandAtomicPseudoInsts.cpp",
    "RISCVExpandPseudoInsts.cpp",
    "RISCVFrameLowering.cpp",
    "RISCVGatherScatterLowering.cpp",
    "RISCVISelDAGToDAG.cpp",
    "RISCVISelLowering.cpp",
    "RISCVInsertReadWriteCSR.cpp",
    "RISCVInsertVSETVLI.cpp",
    "RISCVInstrInfo.cpp",
    "RISCVMachineFunctionInfo.cpp",
    "RISCVMacroFusion.cpp",
    "RISCVMakeCompressible.cpp",
    "RISCVMergeBaseOffset.cpp",
    "RISCVMoveMerger.cpp",
    "RISCVOptWInstrs.cpp",
    "RISCVPushPopOptimizer.cpp",
    "RISCVRVVInitUndef.cpp",
    "RISCVRedundantCopyElimination.cpp",
    "RISCVRegisterInfo.cpp",
    "RISCVSubtarget.cpp",
    "RISCVTargetMachine.cpp",
    "RISCVTargetObjectFile.cpp",
    "RISCVTargetTransformInfo.cpp",
  ]
  configs += llvm_code_override_configs_append
  configs -= llvm_code_override_configs_remove
}

# This is a bit different from most build files: Due to this group
# having the directory's name, "//${llvm_build_root}/secondary/llvm/lib/Target/RISCV" will refer to this
# target, which pulls in the code in this directory *and all subdirectories*.
# For most other directories, "//${llvm_build_root}/secondary/llvm/lib/Foo" only pulls in the code directly
# in "llvm/lib/Foo". The forwarding targets in //${llvm_build_root}/secondary/llvm/lib/Target expect this
# different behavior.
group("RISCV") {
  deps = [
    ":LLVMRISCVCodeGen",
    "AsmParser",
    "Disassembler",
    "MCA",
    "MCTargetDesc",
    "TargetInfo",
  ]
}
