import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

tablegen("SparcGenCallingConv") {
  visibility = [ ":LLVMSparcCodeGen" ]
  args = [ "-gen-callingconv" ]
  td_file = "Sparc.td"
}

tablegen("SparcGenDAGISel") {
  visibility = [ ":LLVMSparcCodeGen" ]
  args = [ "-gen-dag-isel" ]
  td_file = "Sparc.td"
}

static_library("LLVMSparcCodeGen") {
  deps = [
    ":SparcGenCallingConv",
    ":SparcGenDAGISel",
    "MCTargetDesc",
    "TargetInfo",
    "//${llvm_build_root}/secondary/llvm/include/llvm/Config:llvm-config",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/AsmPrinter",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen/SelectionDAG",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]
  include_dirs = [ "." ]
  sources = [
    "DelaySlotFiller.cpp",
    "LeonPasses.cpp",
    "SparcAsmPrinter.cpp",
    "SparcFrameLowering.cpp",
    "SparcISelDAGToDAG.cpp",
    "SparcISelLowering.cpp",
    "SparcInstrInfo.cpp",
    "SparcMCInstLower.cpp",
    "SparcMachineFunctionInfo.cpp",
    "SparcRegisterInfo.cpp",
    "SparcSubtarget.cpp",
    "SparcTargetMachine.cpp",
    "SparcTargetObjectFile.cpp",
  ]
}

# This is a bit different from most build files: Due to this group
# having the directory's name, "//${llvm_build_root}/secondary/llvm/lib/Target/Sparc" will refer to this
# target, which pulls in the code in this directory *and all subdirectories*.
# For most other directories, "//${llvm_build_root}/secondary/llvm/lib/Foo" only pulls in the code directly
# in "llvm/lib/Foo". The forwarding targets in //${llvm_build_root}/secondary/llvm/lib/Target expect this
# different behavior.
group("Sparc") {
  deps = [
    ":LLVMSparcCodeGen",
    "AsmParser",
    "Disassembler",
    "MCTargetDesc",
    "TargetInfo",
  ]
}
