import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/tools/binutils_symlinks.gni")
import("//${llvm_build_root}/build/driver_executable.gni")
import("//${llvm_build_root}/build/symlink_or_copy.gni")

symlinks = [
  "llvm-dlltool",
  "llvm-lib",
  "llvm-ranlib",
]
if (llvm_install_binutils_symlinks) {
  symlinks += [
    "ar",
    "dlltool",
    "ranlib",
  ]
}
foreach(target, symlinks) {
  symlink_or_copy(target) {
    deps = [ ":llvm-ar" ]
    source = "llvm-ar"
    output = "$root_out_dir/bin/$target"
  }
}

# //:llvm-ar depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-ar" ]
  foreach(target, symlinks) {
    deps += [ ":$target" ]
  }
}

driver_executable("llvm-ar") {
  deps = [
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/Object",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target:TargetsToBuild",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
    "//${llvm_build_root}/secondary/llvm/lib/ToolDrivers/llvm-dlltool:DlltoolDriver",
    "//${llvm_build_root}/secondary/llvm/lib/ToolDrivers/llvm-lib:LibDriver",
  ]
  sources = [ "llvm-ar.cpp" ]
}
