import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/tools/binutils_symlinks.gni")
import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}/build/driver_executable.gni")
import("//${llvm_build_root}/build/symlink_or_copy.gni")

tablegen("Opts") {
  visibility = [ ":llvm-cxxfilt" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("cxxfilt") {  # Can't have '+' in target name.
    deps = [ ":llvm-cxxfilt" ]
    source = "llvm-cxxfilt"
    output = "$root_out_dir/bin/c++filt"  # Note: c++filt, not cxxfilt
  }
}

# //:llvm-cxxfilt depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-cxxfilt" ]
  if (llvm_install_binutils_symlinks) {
    deps += [ ":cxxfilt" ]
  }
}

driver_executable("llvm-cxxfilt") {
  deps = [
    ":Opts",
    "//${llvm_build_root}/secondary/llvm/lib/Demangle",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]
  sources = [ "llvm-cxxfilt.cpp" ]
}
