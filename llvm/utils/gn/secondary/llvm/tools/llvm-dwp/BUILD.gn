import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/tools/binutils_symlinks.gni")
import("//${llvm_build_root}/build/symlink_or_copy.gni")
import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

tablegen("Opts") {
  visibility = [ ":llvm-dwp" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("dwp") {
    deps = [ ":llvm-dwp" ]
    source = "llvm-dwp"
    output = "$root_out_dir/bin/dwp"
  }
}

# //:llvm-dwp depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-dwp" ]
  if (llvm_install_binutils_symlinks) {
    deps += [ ":dwp" ]
  }
}

executable("llvm-dwp") {
  deps = [
    ":Opts",
    "//${llvm_build_root}/secondary/llvm/lib/DWP",
    "//${llvm_build_root}/secondary/llvm/lib/DebugInfo/DWARF",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/Object",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target:TargetsToBuild",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]
  sources = [ "llvm-dwp.cpp" ]
}
