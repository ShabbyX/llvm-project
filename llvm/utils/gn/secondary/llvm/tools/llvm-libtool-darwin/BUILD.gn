import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/tools/cctools_symlinks.gni")
import("//${llvm_build_root}/build/symlink_or_copy.gni")
import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")

tablegen("Opts") {
  visibility = [ ":llvm-libtool-darwin" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_cctools_symlinks) {
  symlink_or_copy("libtool") {
    deps = [ ":llvm-libtool-darwin" ]
    source = "llvm-libtool-darwin"
    output = "$root_out_dir/bin/libtool"
  }
}

# //:llvm-libtool-darwin depends on this symlink target, see comment in
# //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-libtool-darwin" ]
  if (llvm_install_cctools_symlinks) {
    deps += [ ":libtool" ]
  }
}

executable("llvm-libtool-darwin") {
  deps = [
    ":Opts",
    "//${llvm_build_root}/secondary/llvm/lib/Object",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target:TargetsToBuild",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]
  sources = [ "llvm-libtool-darwin.cpp" ]
}
