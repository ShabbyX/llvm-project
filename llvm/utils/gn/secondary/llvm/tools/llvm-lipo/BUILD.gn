import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/tools/cctools_symlinks.gni")
import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}/build/driver_executable.gni")
import("//${llvm_build_root}/build/symlink_or_copy.gni")

tablegen("LipoOpts") {
  visibility = [ ":llvm-lipo" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_cctools_symlinks) {
  symlink_or_copy("lipo") {
    deps = [ ":llvm-lipo" ]
    source = "llvm-lipo"
    output = "$root_out_dir/bin/lipo"
  }
}

# //:llvm-lipo depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-lipo" ]
  if (llvm_install_cctools_symlinks) {
    deps += [ ":lipo" ]
  }
}

driver_executable("llvm-lipo") {
  deps = [
    ":LipoOpts",
    "//${llvm_build_root}/secondary/llvm/lib/Object",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target:TargetsToBuild",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]
  sources = [ "llvm-lipo.cpp" ]
}
