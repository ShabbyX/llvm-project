import("//build_overrides/llvm.gni")

import("//${llvm_root}llvm/tools/binutils_symlinks.gni")
import("//${llvm_root}llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}build/driver_executable.gni")
import("//${llvm_build_root}build/symlink_or_copy.gni")

tablegen("Opts") {
  visibility = [ ":llvm-nm" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("nm") {
    deps = [ ":llvm-nm" ]
    source = "llvm-nm"
    output = "$root_out_dir/bin/nm"
  }
}

# //:llvm-nm depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-nm" ]
  if (llvm_install_binutils_symlinks) {
    deps += [ ":nm" ]
  }
}

driver_executable("llvm-nm") {
  deps = [
    ":Opts",
    "//${llvm_root}llvm/lib/Bitcode/Reader",
    "//${llvm_root}llvm/lib/Demangle",
    "//${llvm_root}llvm/lib/IR",
    "//${llvm_root}llvm/lib/Object",
    "//${llvm_root}llvm/lib/Option",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/Target:AllTargetsAsmParsers",
    "//${llvm_root}llvm/lib/Target:AllTargetsDescs",
    "//${llvm_root}llvm/lib/Target:AllTargetsInfos",
    "//${llvm_root}llvm/lib/TargetParser",
  ]
  sources = [ "llvm-nm.cpp" ]
}
