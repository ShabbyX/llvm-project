import("//build_overrides/llvm.gni")

import("//${llvm_root}llvm/tools/binutils_symlinks.gni")
import("//${llvm_root}llvm/tools/cctools_symlinks.gni")
import("//${llvm_root}llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}build/driver_executable.gni")
import("//${llvm_build_root}build/symlink_or_copy.gni")

tablegen("BitcodeStripOpts") {
  visibility = [ ":llvm-objcopy" ]
  args = [ "-gen-opt-parser-defs" ]
}

tablegen("InstallNameToolOpts") {
  visibility = [ ":llvm-objcopy" ]
  args = [ "-gen-opt-parser-defs" ]
}

tablegen("ObjcopyOpts") {
  visibility = [ ":llvm-objcopy" ]
  args = [ "-gen-opt-parser-defs" ]
}

tablegen("StripOpts") {
  visibility = [ ":llvm-objcopy" ]
  args = [ "-gen-opt-parser-defs" ]
}

symlinks = [
  "llvm-bitcode-strip",
  "llvm-install-name-tool",
  "llvm-strip",
]
if (llvm_install_binutils_symlinks) {
  symlinks += [
    "objcopy",
    "strip",
  ]
}
if (llvm_install_cctools_symlinks) {
  symlinks += [
    "bitcode_strip",
    "install_name_tool",
  ]
}
foreach(target, symlinks) {
  symlink_or_copy(target) {
    deps = [ ":llvm-objcopy" ]
    source = "llvm-objcopy"
    output = "$root_out_dir/bin/$target"
  }
}

# //:llvm-objcopy depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-objcopy" ]
  foreach(target, symlinks) {
    deps += [ ":$target" ]
  }
}

driver_executable("llvm-objcopy") {
  deps = [
    ":BitcodeStripOpts",
    ":InstallNameToolOpts",
    ":ObjcopyOpts",
    ":StripOpts",
    "//${llvm_root}llvm/lib/MC",
    "//${llvm_root}llvm/lib/ObjCopy",
    "//${llvm_root}llvm/lib/Object",
    "//${llvm_root}llvm/lib/Option",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/TargetParser",
  ]
  sources = [
    "ObjcopyOptions.cpp",
    "llvm-objcopy.cpp",
  ]
}
