import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/tools/binutils_symlinks.gni")
import("//${llvm_build_root}/secondary/llvm/tools/cctools_symlinks.gni")
import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}/build/symlink_or_copy.gni")

tablegen("ObjdumpOpts") {
  visibility = [ ":llvm-objdump" ]
  args = [ "-gen-opt-parser-defs" ]
}

tablegen("OtoolOpts") {
  visibility = [ ":llvm-objdump" ]
  args = [ "-gen-opt-parser-defs" ]
}

symlinks = [ "llvm-otool" ]
if (llvm_install_binutils_symlinks) {
  symlinks += [ "objdump" ]
}
if (llvm_install_cctools_symlinks) {
  symlinks += [ "otool" ]
}

foreach(target, symlinks) {
  symlink_or_copy(target) {
    deps = [ ":llvm-objdump" ]
    source = "llvm-objdump"
    output = "$root_out_dir/bin/$target"
  }
}

# //:llvm-objdump depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-objdump" ]
  foreach(target, symlinks) {
    deps += [ ":$target" ]
  }
}

executable("llvm-objdump") {
  deps = [
    ":ObjdumpOpts",
    ":OtoolOpts",
    "//${llvm_build_root}/secondary/llvm/include/llvm/Config:config",
    "//${llvm_build_root}/secondary/llvm/lib/CodeGen",
    "//${llvm_build_root}/secondary/llvm/lib/DebugInfo/DWARF",
    "//${llvm_build_root}/secondary/llvm/lib/DebugInfo/PDB",
    "//${llvm_build_root}/secondary/llvm/lib/DebugInfo/Symbolize",
    "//${llvm_build_root}/secondary/llvm/lib/Debuginfod",
    "//${llvm_build_root}/secondary/llvm/lib/Demangle",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/MC/MCDisassembler",
    "//${llvm_build_root}/secondary/llvm/lib/Object",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target:AllTargetsDescs",
    "//${llvm_build_root}/secondary/llvm/lib/Target:AllTargetsDisassemblers",
    "//${llvm_build_root}/secondary/llvm/lib/Target:AllTargetsInfos",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
    "//${llvm_build_root}/build/libs/xar",
  ]
  sources = [
    "COFFDump.cpp",
    "ELFDump.cpp",
    "MachODump.cpp",
    "OffloadDump.cpp",
    "SourcePrinter.cpp",
    "WasmDump.cpp",
    "XCOFFDump.cpp",
    "llvm-objdump.cpp",
  ]
}
