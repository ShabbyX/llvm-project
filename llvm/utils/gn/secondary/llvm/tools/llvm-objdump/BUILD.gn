import("//build_overrides/llvm.gni")

import("//${llvm_root}llvm/tools/binutils_symlinks.gni")
import("//${llvm_root}llvm/tools/cctools_symlinks.gni")
import("//${llvm_root}llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}build/symlink_or_copy.gni")

tablegen("ObjdumpOpts") {
  visibility = [ ":llvm-objdump" ]
  args = [ "-gen-opt-parser-defs" ]
}

tablegen("OtoolOpts") {
  visibility = [ ":llvm-objdump" ]
  args = [ "-gen-opt-parser-defs" ]
}

symlinks = [ "llvm-otool" ]
if (llvm_install_binutils_symlinks) {
  symlinks += [ "objdump" ]
}
if (llvm_install_cctools_symlinks) {
  symlinks += [ "otool" ]
}

foreach(target, symlinks) {
  symlink_or_copy(target) {
    deps = [ ":llvm-objdump" ]
    source = "llvm-objdump"
    output = "$root_out_dir/bin/$target"
  }
}

# //:llvm-objdump depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-objdump" ]
  foreach(target, symlinks) {
    deps += [ ":$target" ]
  }
}

executable("llvm-objdump") {
  deps = [
    ":ObjdumpOpts",
    ":OtoolOpts",
    "//${llvm_root}llvm/include/llvm/Config:config",
    "//${llvm_root}llvm/lib/CodeGen",
    "//${llvm_root}llvm/lib/DebugInfo/DWARF",
    "//${llvm_root}llvm/lib/DebugInfo/PDB",
    "//${llvm_root}llvm/lib/DebugInfo/Symbolize",
    "//${llvm_root}llvm/lib/Debuginfod",
    "//${llvm_root}llvm/lib/Demangle",
    "//${llvm_root}llvm/lib/MC",
    "//${llvm_root}llvm/lib/MC/MCDisassembler",
    "//${llvm_root}llvm/lib/Object",
    "//${llvm_root}llvm/lib/Option",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/Target:AllTargetsDescs",
    "//${llvm_root}llvm/lib/Target:AllTargetsDisassemblers",
    "//${llvm_root}llvm/lib/Target:AllTargetsInfos",
    "//${llvm_root}llvm/lib/TargetParser",
    "//${llvm_build_root}build/libs/xar",
  ]
  sources = [
    "COFFDump.cpp",
    "ELFDump.cpp",
    "MachODump.cpp",
    "OffloadDump.cpp",
    "SourcePrinter.cpp",
    "WasmDump.cpp",
    "XCOFFDump.cpp",
    "llvm-objdump.cpp",
  ]
}
