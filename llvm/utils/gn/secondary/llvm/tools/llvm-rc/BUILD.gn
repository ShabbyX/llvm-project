import("//build_overrides/llvm.gni")

import("//${llvm_root}llvm/tools/binutils_symlinks.gni")
import("//${llvm_root}llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}build/driver_executable.gni")
import("//${llvm_build_root}build/symlink_or_copy.gni")

tablegen("Opts") {
  visibility = [ ":llvm-rc" ]
  args = [ "-gen-opt-parser-defs" ]
}

tablegen("WindresOpts") {
  visibility = [ ":llvm-rc" ]
  args = [ "-gen-opt-parser-defs" ]
}

symlinks = [ "llvm-windres" ]
if (llvm_install_binutils_symlinks) {
  symlinks += [ "windres" ]
}

foreach(target, symlinks) {
  symlink_or_copy(target) {
    deps = [ ":llvm-rc" ]
    source = "llvm-rc"
    output = "$root_out_dir/bin/$target"
  }
}

# //:llvm-rc depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-rc" ]
  foreach(target, symlinks) {
    deps += [ ":$target" ]
  }
}

driver_executable("llvm-rc") {
  deps = [
    ":Opts",
    ":WindresOpts",
    "//${llvm_root}llvm/lib/Object",
    "//${llvm_root}llvm/lib/Option",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/TargetParser",
  ]
  sources = [
    "ResourceFileWriter.cpp",
    "ResourceScriptCppFilter.cpp",
    "ResourceScriptParser.cpp",
    "ResourceScriptStmt.cpp",
    "ResourceScriptToken.cpp",
    "llvm-rc.cpp",
  ]
}
