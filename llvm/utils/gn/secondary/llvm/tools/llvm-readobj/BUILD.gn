import("//build_overrides/llvm.gni")

import("//${llvm_root}llvm/tools/binutils_symlinks.gni")
import("//${llvm_root}llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}build/driver_executable.gni")
import("//${llvm_build_root}build/symlink_or_copy.gni")

tablegen("Opts") {
  visibility = [ ":llvm-readobj" ]
  args = [ "-gen-opt-parser-defs" ]
}

symlinks = [ "llvm-readelf" ]
if (llvm_install_binutils_symlinks) {
  symlinks += [ "readelf" ]
}
foreach(target, symlinks) {
  symlink_or_copy(target) {
    deps = [ ":llvm-readobj" ]
    source = "llvm-readobj"
    output = "$root_out_dir/bin/$target"
  }
}

# //:llvm-readobj depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-readobj" ]
  foreach(target, symlinks) {
    deps += [ ":$target" ]
  }
}

driver_executable("llvm-readobj") {
  deps = [
    ":Opts",
    "//${llvm_root}llvm/lib/BinaryFormat",
    "//${llvm_root}llvm/lib/DebugInfo/CodeView",
    "//${llvm_root}llvm/lib/DebugInfo/DWARF",
    "//${llvm_root}llvm/lib/DebugInfo/MSF",
    "//${llvm_root}llvm/lib/DebugInfo/PDB",
    "//${llvm_root}llvm/lib/Object",
    "//${llvm_root}llvm/lib/Option",
    "//${llvm_root}llvm/lib/Support",
    "//${llvm_root}llvm/lib/TargetParser",
  ]
  sources = [
    "ARMWinEHPrinter.cpp",
    "COFFDumper.cpp",
    "COFFImportDumper.cpp",
    "ELFDumper.cpp",
    "MachODumper.cpp",
    "ObjDumper.cpp",
    "WasmDumper.cpp",
    "Win64EHDumper.cpp",
    "WindowsResourceDumper.cpp",
    "XCOFFDumper.cpp",
    "llvm-readobj.cpp",
  ]
}
