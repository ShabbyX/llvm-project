import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/tools/binutils_symlinks.gni")
import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}/build/driver_executable.gni")
import("//${llvm_build_root}/build/symlink_or_copy.gni")

tablegen("Opts") {
  visibility = [ ":llvm-size" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("size") {
    deps = [ ":llvm-size" ]
    source = "llvm-size"
    output = "$root_out_dir/bin/size"
  }
}

# //:llvm-size depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-size" ]
  if (llvm_install_binutils_symlinks) {
    deps += [ ":size" ]
  }
}

driver_executable("llvm-size") {
  deps = [
    ":Opts",
    "//${llvm_build_root}/secondary/llvm/lib/Object",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/TargetParser",
  ]
  sources = [ "llvm-size.cpp" ]
}
