import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/llvm/tools/binutils_symlinks.gni")
import("//${llvm_build_root}/secondary/llvm/utils/TableGen/tablegen.gni")
import("//${llvm_build_root}/build/symlink_or_copy.gni")

tablegen("Opts") {
  visibility = [ ":llvm-strings" ]
  args = [ "-gen-opt-parser-defs" ]
}

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("strings") {
    deps = [ ":llvm-strings" ]
    source = "llvm-strings"
    output = "$root_out_dir/bin/strings"
  }
}

# //:llvm-strings depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-strings" ]
  if (llvm_install_binutils_symlinks) {
    deps += [ ":strings" ]
  }
}

executable("llvm-strings") {
  deps = [
    ":Opts",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/Object",
    "//${llvm_build_root}/secondary/llvm/lib/Option",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
  ]
  sources = [ "llvm-strings.cpp" ]
}
