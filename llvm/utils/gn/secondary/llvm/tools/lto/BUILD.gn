import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/build/symbol_exports.gni")
import("//${llvm_build_root}/secondary/llvm/version.gni")

symbol_exports("exports") {
  exports_file = "lto.exports"
}

shared_library("lto") {
  output_name = "LTO"
  deps = [
    ":exports",
    "//${llvm_build_root}/secondary/llvm/lib/Bitcode/Reader",
    "//${llvm_build_root}/secondary/llvm/lib/IR",
    "//${llvm_build_root}/secondary/llvm/lib/LTO",
    "//${llvm_build_root}/secondary/llvm/lib/MC",
    "//${llvm_build_root}/secondary/llvm/lib/MC/MCDisassembler",
    "//${llvm_build_root}/secondary/llvm/lib/Support",
    "//${llvm_build_root}/secondary/llvm/lib/Target",
    "//${llvm_build_root}/secondary/llvm/lib/Target:TargetsToBuild",
  ]
  sources = [
    "LTODisassembler.cpp",
    "lto.cpp",
  ]

  if (host_os == "mac") {
    ldflags = [
      "-Wl,-compatibility_version,1",
      "-Wl,-current_version,$llvm_version",

      # See llvm_setup_rpath() in CMake.
      "-Wl,-install_name,@rpath/libLTO.dylib",
      "-Wl,-rpath,@loader_path/../lib",
    ]
  }
}
