import("//build_overrides/llvm.gni")

import("//${llvm_build_root}/secondary/bolt/test/bolt_lit_site_cfg_files.gni")
import("//${llvm_build_root}/secondary/clang-tools-extra/clangd/test/clangd_lit_site_cfg_files.gni")
import("//${llvm_build_root}/secondary/clang-tools-extra/pseudo/test/clang_pseudo_lit_site_cfg_files.gni")
import("//${llvm_build_root}/secondary/clang-tools-extra/test/clang_tools_extra_lit_site_cfg_files.gni")
import("//${llvm_build_root}/secondary/clang/test/clang_lit_site_cfg_files.gni")
import("//${llvm_build_root}/secondary/lld/test/lld_lit_site_cfg_files.gni")
import("//${llvm_build_root}/secondary/lldb/test/lldb_lit_site_cfg_files.gni")
import("//${llvm_build_root}/secondary/llvm/test/llvm_lit_site_cfg_files.gni")
import("//${llvm_build_root}/build/write_cmake_config.gni")
import("//${llvm_build_root}/secondary/llvm/utils/llvm-lit/lit_path_function.gni")

write_cmake_config("llvm-lit") {
  input = "llvm-lit.in"
  output = "$root_out_dir/bin/llvm-lit"

  if (host_os == "win") {
    # llvm-lit needs suffix.py for multiprocess to find a main module.
    output = "${output}.py"
  }

  # lit's lit/llvm/config.py shells out to llvm-config.
  deps = [ "//${llvm_build_root}/secondary/llvm/tools/llvm-config" ]

  deps += [
    "//${llvm_build_root}/secondary/clang-tools-extra/clangd/test:lit_site_cfg",
    "//${llvm_build_root}/secondary/clang-tools-extra/clangd/test:lit_unit_site_cfg",
    "//${llvm_build_root}/secondary/clang-tools-extra/pseudo/test:lit_site_cfg",
    "//${llvm_build_root}/secondary/clang-tools-extra/pseudo/test:lit_unit_site_cfg",
    "//${llvm_build_root}/secondary/clang-tools-extra/test:lit_site_cfg",
    "//${llvm_build_root}/secondary/clang-tools-extra/test:lit_unit_site_cfg",
    "//${llvm_build_root}/secondary/clang/test:lit_site_cfg",
    "//${llvm_build_root}/secondary/clang/test:lit_unit_site_cfg",
    "//${llvm_build_root}/secondary/lld/test:lit_site_cfg",
    "//${llvm_build_root}/secondary/lld/test:lit_unit_site_cfg",
    "//${llvm_build_root}/secondary/lldb/test:lit_api_site_cfg",
    "//${llvm_build_root}/secondary/lldb/test:lit_shell_site_cfg",
    "//${llvm_build_root}/secondary/lldb/test:lit_site_cfg",
    "//${llvm_build_root}/secondary/lldb/test:lit_unit_site_cfg",
    "//${llvm_build_root}/secondary/llvm/test:lit_site_cfg",
    "//${llvm_build_root}/secondary/llvm/test:lit_unit_site_cfg",
  ]

  entries = [
    [
      "//${llvm_build_root}/secondary/bolt/test/lit.cfg.py",
      bolt_lit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/bolt/test/Unit/lit.cfg.py",
      bolt_lit_unit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/clang-tools-extra/test/lit.cfg.py",
      clang_tools_extra_lit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/clang-tools-extra/test/Unit/lit.cfg.py",
      clang_tools_extra_lit_unit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/clang-tools-extra/clangd/test/lit.cfg.py",
      clangd_lit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/clang-tools-extra/clangd/unittests/lit.cfg.py",
      clangd_lit_unit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/clang-tools-extra/pseudo/test/lit.cfg.py",
      clang_pseudo_lit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/clang-tools-extra/pseudo/test/Unit/lit.cfg.py",
      clang_pseudo_lit_unit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/clang/test/lit.cfg.py",
      clang_lit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/clang/test/Unit/lit.cfg.py",
      clang_lit_unit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/lld/test/lit.cfg.py",
      lld_lit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/lld/test/Unit/lit.cfg.py",
      lld_lit_unit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/lldb/test/lit.cfg.py",
      lldb_lit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/lldb/test/API/lit.cfg.py",
      lldb_lit_api_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/lldb/test/Shell/lit.cfg.py",
      lldb_lit_shell_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/lldb/test/Unit/lit.cfg.py",
      lldb_lit_unit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/llvm/test/lit.cfg.py",
      llvm_lit_site_cfg_file,
    ],
    [
      "//${llvm_build_root}/secondary/llvm/test/Unit/lit.cfg.py",
      llvm_lit_unit_site_cfg_file,
    ],
  ]

  # Generate LLVM_LIT_CONFIG_MAP parameter.
  # llvm-lit contains a mapping from each lit.cfg.py file to the corresponding
  # generated llvm.site.cfg.py file, so llvm-lit depends on all the targets to
  # generate the site.cfg.py file.
  # Note: \n is converted to a newline by write_cmake_config.py, not by gn.
  config_map = lit_path_function + "\n"
  dir = get_path_info(output, "dir")
  foreach(entry, entries) {
    config_map += "\nmap_config(\n    path(r'" + rebase_path(entry[0], dir) +
                  "'),\n    path(r'" + rebase_path(entry[1], dir) + "'))"
  }

  values = [
    "LLVM_SOURCE_DIR=" + rebase_path("//llvm", dir),
    "Python3_EXECUTABLE=/usr/bin/env $python_path",
    "BUILD_MODE=.",
    "LLVM_LIT_CONFIG_MAP=" + config_map,
  ]
}
